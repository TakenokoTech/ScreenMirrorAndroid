apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "tech.takenoko.screenmirror"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile       rootProject.file("release.jks")
            storePassword   System.getenv("RELEASE_STORE_PASSWORD")
            keyAlias        System.getenv("RELEASE_KEY_ALIAS")
            keyPassword     System.getenv("RELEASE_KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            debuggable          true
            testCoverageEnabled true
        }
        release {
            signingConfig       signingConfigs.release
            debuggable          false
            minifyEnabled       true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    implementation 'com.google.android.material:material:1.1.0-alpha10'


    // ViewModel and LiveData
    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:viewmodel:$lifecycle_version"
    implementation "android.arch.lifecycle:livedata:$lifecycle_version"
    implementation "android.arch.lifecycle:runtime:$lifecycle_version"
    implementation "android.arch.lifecycle:reactivestreams:$lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"
    testImplementation "android.arch.core:core-testing:$lifecycle_version"

    // Coroutine
    def coroutines_version = "1.3.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Network
    implementation "org.java-websocket:Java-WebSocket:1.3.9"

    // QR code
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // gRPC
    implementation 'io.grpc:grpc-okhttp:1.24.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.24.0'
    implementation 'io.grpc:grpc-stub:1.24.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation "javax.annotation:javax.annotation-api:1.3.2"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }
    plugins {
        lite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.24.0" }
    }
    generateProtoTasks {
        all().each { task -> task.plugins {
            lite {}
            grpc { option("lite") }
        } }
    }
}